/*!
 *  BayLang Technology
 *
 *  (c) Copyright 2016-2025 "Ildar Bikmamatov" <support@bayrell.org>
 *
 *  Licensed under the Apache License, Version 2.0 (the "License");
 *  you may not use this file except in compliance with the License.
 *  You may obtain a copy of the License at
 *
 *      https://www.apache.org/licenses/LICENSE-2.0
 *
 *  Unless required by applicable law or agreed to in writing, software
 *  distributed under the License is distributed on an "AS IS" BASIS,
 *  WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 *  See the License for the specific language governing permissions and
 *  limitations under the License.
 */

namespace Runtime.Unit;

use Runtime.Entity;


class AssertHelper
{
	/**
	 * Check equals of types
	 */
	static void equalValueType(var value1, var value2)
	{
		string message = "Type mismatch \"" ~ value1 ~ "\" and \"" ~ value2 ~ "\"";
		string type1 = rtl::getType(value1);
		string type2 = rtl::getType(value2);
		rtl::assert(type1 == type2, message);
	}
	
	
	/**
	 * Check equals of types
	 */
	static void equalType(var value1, string type1)
	{
		string type2 = rtl::getType(value1);
		string message = "Type mismatch. Needs \"" ~ type1 ~ "\". Exists \"" ~ type2 ~ "\"";
		rtl::assert(type1 == type2, message);
	}
	
	
	/**
	 * Check equals of values
	 */
	static void equalValue(var value1, var value2, string message = "")
	{
		static::equalValueType(value1, value2);
		
		if (value1 instanceof Vector)
		{
			static::equalVector(value1, value2);
			return;
		}
		
		if (value1 instanceof Map)
		{
			static::equalMap(value1, value2);
			return;
		}
		
		if (message == "") message = "\"" ~ value1 ~ "\" != \"" ~ value2 ~ "\"";
		rtl::assert(value1 === value2, message);
	}
	
	
	/**
	 * Check equals of two vectors
	 */
	static void equalVector(Vector c1, Vector c2)
	{
		if (c1.count() != c2.count())
		{
			string message = "Vectors has different counts";
			rtl::assert(false, message);
		}
		
		for (int i=0; i<c1.count(); i++)
		{
			var value1 = c1.get(i);
			var value2 = c2.get(i);
			
			static::equalValue(value1, value2);
		}
	}
	
	
	/**
	 * Check equals of two maps
	 */
	static void equalMap(Map d1, Map d2)
	{
		Vector d1_keys = rtl::list(d1.keys());
		Vector d2_keys = rtl::list(d2.keys());
		
		for (int i=0; i<d1_keys.count(); i++)
		{
			string key1 = d1_keys.get(i);
			
			if (not d2.has(key1))
			{
				string message = "Map does not has key \"" ~ key1 ~ "\"";
				rtl::assert(false, message);
			}
			
			var value1 = d1.get(key1);
			var value2 = d2.get(key1);
			
			static::equalValue(value1, value2);
		}
		
		for (int i=0; i<d2_keys.count(); i++)
		{
			string key2 = d2_keys.get(i);
			
			if (not d1.has(key2))
			{
				string message = "Map does not has key \"" ~ key2 ~ "\"";
				rtl::assert(false, message);
			}
		}
	}
}